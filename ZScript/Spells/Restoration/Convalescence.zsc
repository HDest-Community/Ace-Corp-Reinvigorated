class OldConvalescenceSpell : OldArcanumSpell
{
	override int GetIndex() { return 0; }
	override class<OldArcanumSpellTree> GetTree() { return 'OldArcanumTreeRestoration'; }
	override string GetName() { return "Convalescence"; }
	override int GetSpellType() { return SType_SelfOrMass | SType_Targeted; }
	override int, int GetCastRequirements() { return 100, 0; }
	override string GetDescription()
	{
		string plural = Level < GetMaxLevel() ? "s" : "";
		return String.Format("Recover 1 health every \c[Gold]%i\c- heartbeat%s for 3 minutes. Prevents pain jolts from second flesh for the duration of the effect. Helps with zerk comedown.", 4 - Level, plural);
	}
	override string GetTechnicalInfo() { return "Self, Targeted, Touch"; }
	override int GetExperienceGain() { return 2; }
	override class<OldArcanumSigil> GetSigil() { return 'OldConvalescenceSigil'; }
	override string, class<Powerup> GetIndicatorInfo() { return "INDCONVL", 'OldConvalescenceEffect'; }
}

class OldConvalescenceSigil : OldArcanumSigil
{
	override void ActivateTargeted()
	{
		Actor a = AimTarget();
		if (a && a.Health > 0 && Distance3D(a) <= HDCONST_ONEMETRE * 2)
		{
			DoHealing(a);
		}
	}

	override void ActivateSelfOrMass()
	{
		if (master)
		{
			DoHealing(master);
		}
	}

	private void DoHealing(Actor a)
	{
		PlayCastSound(self, "Arcanum/Restoration/Convalescence");
		a.A_GiveInventory("OldConvalescenceEffect");
		let Effect = OldConvalescenceEffect(a.FindInventory("OldConvalescenceEffect"));
		Effect.Level = SigilLevel;
	}

	override void InitRuneSlots()
	{
		vector2 Size = (4, 6.66);
		CreateRuneSlot((0, 13.4), Size, 1.0);
		CreateRuneSlot((0, -13.4), Size, 1.0);
		CreateRuneSlot((-15, 0), Size, 1.0);
		CreateRuneSlot((15, 0), Size, 1.0);
	}

	Default
	{
		OldArcanumSigil.FadeOutSpeedMult 2.0;
		StencilColor "FF9E9E";
	}

	States
	{
		Spawn:
			CRRE A 0;
			Goto Super::Spawn;
	}
}

class OldConvalescenceEffect : Powerup
{
	override void DoEffect()
	{
		let plr = HDPlayerPawn(owner);
		if (plr && plr.beatcount == 0 && plr.beatcounter % (4 - Level) == 0)
		{
			if (plr.Health < plr.default.Health)
			{
				plr.GiveBody(1);
			}
			int zerk = plr.CountInv('HDZerk');
			if (zerk > 0 && zerk <= HDZerk.HDZERK_COOLOFF)
			{
				plr.A_TakeInventory('HDZerk', 50);
			}
		}
		else if (!plr && owner && GetAge() % 20 == 0)
		{
			let mob = HDMobBase(owner);
			if (mob)
			{
				mob.BodyDamage = max(0, mob.BodyDamage - Level * 2);
			}
			owner.GiveBody(Level * 2);
		}

		Super.DoEffect();
	}

	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if (damageType == 'staples')
		{
			damageType = 'None';
			newdamage = 0;
		}
		Super.ModifyDamage(damage, damageType, newdamage, passive, inflictor, source, flags);
	}

	int Level;

	Default
	{
		+INVENTORY.ALWAYSPICKUP
		Powerup.Duration -180;
	}
}